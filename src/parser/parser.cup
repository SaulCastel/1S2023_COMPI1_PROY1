package parser;
import java_cup.runtime.*;
import exregan.lib.*;
import java.util.LinkedList;
import java.util.HashMap;

parser code
{:
  int position = 1;
  int nodeID = 1;
  HashMap<Integer, String> symbols = new HashMap<>();
  HashMap<Integer, LinkedList> nextTable = new HashMap<>();
  HashMap<String, FSM> automata = new HashMap<>();
  FiniteStateMachine generator = new FiniteStateMachine();
  Graph graph = new Graph();

  public void syntax_error(Symbol s){
    System.out.println("Error recuperabe de sintaxis: "+ s.value +" en linea "+(s.left+1)+", columna "+(s.right+1) );
  }

  public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
    System.out.println("Error no recuperabe de sintaxis: "+ s.value +" en linea "+(s.left+1)+", columna "+(s.right+1) );
  }
:}

terminal String LBRACKET, SEPARATOR, RBRACKET;
terminal String CONJ, COLON, IDENTIFIER, HYPHEN, GREATER, SEMICOLON;
terminal String TILDE, COMMA;
terminal String LOWERC, UPPERC, NUMBER, ASCII;
terminal String UNION, CONCAT, OPTION, ITERATION, KLEENE;
terminal String NEWLINE, QUOTES, APOSTROPHE, STRING;

non terminal beginning, instruction;
non terminal defineSet;
non terminal expression;
non terminal groups, charRange, list, alphabet;
non terminal regex, setCall, tests, test;

start with beginning;

beginning ::= 
    LBRACKET instruction SEPARATOR SEPARATOR tests RBRACKET
  ;

instruction ::= 
    instruction defineSet
  | defineSet
  | instruction expression
  | expression
  ;

alphabet ::= 
    LOWERC:str
    {:
      RESULT = str;
    :}
  | UPPERC:str
    {:
      RESULT = str;
    :}
  | NUMBER:str
    {:
      RESULT = str;
    :}
  | ASCII:str
    {:
      RESULT = str;
    :}
  | STRING:str
    {:
      RESULT = str;
    :}
  | QUOTES:str
    {:
      RESULT = str;
    :}
  | APOSTROPHE:str
    {:
      RESULT = str;
    :}
  | NEWLINE
    {:
      RESULT = "nl";
    :}
  | error
  ;

setCall ::= 
    LBRACKET IDENTIFIER:id RBRACKET
    {:
      String identifier = "{"+id+"}";
      RESULT = identifier; 
    :}
  ;

defineSet ::= 
    CONJ COLON IDENTIFIER HYPHEN GREATER groups SEMICOLON
  ;

groups ::= 
    charRange
  | list
  ;

charRange ::=
    LOWERC TILDE LOWERC
  | UPPERC TILDE UPPERC
  | NUMBER TILDE NUMBER
  ;

list ::=
    list alphabet COMMA
  | alphabet
  ;

expression ::=
    IDENTIFIER:idRegex HYPHEN GREATER regex:a SEMICOLON
    {:
      RegexNode re = (RegexNode)a;
      RegexNode extension = new RegexNode("#", position, nodeID, null, null);
      nodeID++;
      extension.isNull = false;
      extension.first.add(position);
      extension.last.add(position);
      symbols.put(position, "#");
      RegexNode root = new RegexNode(".", 0, nodeID, re, extension);
      root.isNull = false;
      root.first.addAll(re.first);
      if (re.isNull){
        root.first.addAll(extension.first);
      }
      root.last.addAll(extension.last);
      LinkedList nextList;
      for (int position : re.last){
        if (!nextTable.containsKey(position)){
          nextTable.put(position, new LinkedList<Integer>());
        }
        nextList = nextTable.get(position);
        for (int value : extension.first) {
          if (!nextList.contains(value)){
            nextList.add(value);
          }
        }
      }
      FSM fsm = generator.getFSM(symbols, nextTable, re.first, position);
      automata.put(idRegex, fsm);
      //Generar reportes
      graph.graphTree(root, idRegex);
      graph.graphAFD(fsm, idRegex);
      //Resetear almacenamiento temporal
      position = 1;
      nodeID = 0;
      symbols.clear();
      nextTable.clear();
      System.out.println("Procesamiento de regex terminado: "+idRegex);
    :}
  ;

regex ::=
    UNION:n regex:a regex:b
    {:
      RegexNode lchild = (RegexNode)a;
      RegexNode rchild = (RegexNode)b;
      RegexNode node = new RegexNode(n, 0, nodeID, lchild, rchild);
      nodeID++;
      if (lchild.isNull || rchild.isNull){
        node.isNull = true;
      }
      node.first.addAll(lchild.first);
      node.first.addAll(rchild.first);
      node.last.addAll(lchild.last);
      node.last.addAll(rchild.last);
      RESULT = node;
    :}
  | CONCAT:n regex:a regex:b
    {:
      RegexNode lchild = (RegexNode)a;
      RegexNode rchild = (RegexNode)b;
      RegexNode node = new RegexNode(n, 0, nodeID, lchild, rchild);
      nodeID++;
      if (lchild.isNull && rchild.isNull){
        node.isNull = true;
      }
      node.first.addAll(lchild.first);
      if (lchild.isNull){
        node.first.addAll(rchild.first);
      }
      node.last.addAll(rchild.last);
      if (rchild.isNull){
        node.last.addAll(lchild.last);
      }
      LinkedList nextList;
      for (int position : lchild.last){
        if (!nextTable.containsKey(position)){
          nextTable.put(position, new LinkedList<Integer>());
        }
        nextList = nextTable.get(position);
        for (int value : rchild.first) {
          if (!nextList.contains(value)){
            nextList.add(value);
          }
        }
      }
      RESULT = node;
    :}
  | OPTION:n regex:a
    {:
      RegexNode child = (RegexNode)a;
      RegexNode node = new RegexNode(n, 0, nodeID, child, null);
      nodeID++;
      node.isNull = true;
      node.first.addAll(child.first);
      node.last.addAll(child.last);
      RESULT = node;
    :}
  | ITERATION:n regex:a
    {:
      RegexNode child = (RegexNode)a;
      RegexNode node = new RegexNode(n, 0, nodeID, child, null);
      nodeID++;
      if (child.isNull){
        node.isNull = true;
      }
      node.first.addAll(child.first);
      node.last.addAll(child.last);
      LinkedList nextList;
      for (int position : child.last){
        if (!nextTable.containsKey(position)){
          nextTable.put(position, new LinkedList<Integer>());
        }
        nextList = nextTable.get(position);
        for (int value : child.first) {
          if (!nextList.contains(value)){
            nextList.add(value);
          }
        }
      }
      RESULT = node;
    :}
  | KLEENE:n regex:a
    {:
      RegexNode child = (RegexNode)a;
      RegexNode node = new RegexNode(n, 0, nodeID, child, null);
      nodeID++;
      node.isNull = true;
      node.first.addAll(child.first);
      node.last.addAll(child.last);
      LinkedList nextList;
      for (int position : child.last){
        if (!nextTable.containsKey(position)){
          nextTable.put(position, new LinkedList<Integer>());
        }
        nextList = nextTable.get(position);
        for (int value : child.first) {
          if (!nextList.contains(value)){
            nextList.add(value);
          }
        }
      }
      RESULT = node;
    :}
  | alphabet:n
    {:
      String symbol = (String)n;
      RegexNode node = new RegexNode(symbol, position, nodeID, null, null);
      symbols.put(position, symbol);
      nodeID++;
      node.isNull = false;
      node.first.add(position);
      node.last.add(position);
      position++;
      RESULT = node;
    :}
  | setCall:n
    {:
      String symbol = (String)n;
      RegexNode node = new RegexNode(symbol, position, nodeID, null, null);
      symbols.put(position, symbol);
      nodeID++;
      node.isNull = false;
      node.first.add(position);
      node.last.add(position);
      position++;
      RESULT = node;
    :}
  ;

tests ::= 
    tests test
  | test
  ;

test ::= 
    IDENTIFIER COLON STRING SEMICOLON
  ;
